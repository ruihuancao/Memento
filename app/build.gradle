apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        applicationId "com.memento.android"
        minSdkVersion 21
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi', 'armeabi-v7a', 'x86'//, 'x86_64', 'arm64-v8a'
        }

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }


    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        ignoreWarnings true
        disable 'InvalidPackage'
        disable 'OldTargetApi'
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    signingConfigs {
        debug {
            storeFile file("android.keystore")
            storePassword keystore_passwd
            keyAlias "android.keystore"
            keyPassword keystore_passwd
        }

        // 生成keystore
        // keytool -genkey -alias android.keystore -keyalg RSA -validity 20000 -keystore android.keystore
        release {
            storeFile file("android.keystore")
            storePassword keystore_passwd
            keyAlias "android.keystore"
            keyPassword keystore_passwd
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            // 开启混淆
            minifyEnabled false
            // 混淆文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // 版本名前缀
            versionNameSuffix ""
            // 开启ZipAlign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
        }
        debug {
            signingConfig signingConfigs.debug
            // 开启混淆
            minifyEnabled false
            // 混淆文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // 版本名前缀
            versionNameSuffix "-debug"
            // 不开启ZipAlign优化
            zipAlignEnabled false
            // 不移除无用的resource文件
            shrinkResources false
        }
    }


    productFlavors.all { flavor ->
        manifestPlaceholders.put("LEANCLOUD_CHANNEL_VALUE", name)
    }

    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
        }
        Xiaomi {}
    }

    applicationVariants.all {
        variant ->
            variant.outputs.each {
                output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = outputFile.name.replace(".apk", "-${defaultConfig.versionName}.apk")
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
            }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':assistlibrary')

    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.2') {
        exclude group: 'com.android.support'
    }

    compile 'com.miguelcatalan:materialsearchview:1.0.0'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.github.PhilJay:MPAndroidChart:v2.2.4'
    apt 'com.jakewharton:butterknife-compiler:8.2.1'
    compile 'com.jakewharton:butterknife:8.2.1'
    compile 'com.aurelhubert:ahbottomnavigation:1.2.3'

    compile 'com.tencent.bugly:crashreport:latest.release'

    compile 'com.theartofdev.edmodo:android-image-cropper:2.1.4'
    compile 'pub.devrel:easypermissions:0.1.7'

    compile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.2'
    androidTestCompile 'com.android.support:support-annotations:24.2.1'
}

